/*=========================================================
Garrett the Scavenger
by Ozcodex
===========================================================
Description:
A shrewd merchant with an eye for hidden value.
He sees potential in items others might overlook,
selecting two different items each day to purchase
at premium prices.
===========================================================
Compatibility:
Optimised for Hercules emulators.
===========================================================
Changelog:
v1.0 - First version.
v1.1 - Added price limits to maintain game economy
v2.0 - Major refactor:
       - Changed to 2 items (1 common, 1 misc) instead of 3
       - Improved item selection to avoid duplicates
       - Added daily spending tracking and limits
       - Added historical spending tracking
       - Improved price calculation with configurable increment
       - Enhanced user interface and item display
       - Added option to check total historical spending
       - Better handling of insufficient items case
v2.1 - Character enhancement:
       - Renamed to Garrett the Scavenger
       - Added more personality to dialogues
       - Improved item inspection narrative
v2.2 - Fixed bug where the NPC changes items every relog
=========================================================*/

geffen,143,70,3	script	Garrett	1_M_SIGN1,{
	
	/*-----------------------------------------------------
	Script
	-----------------------------------------------------*/
	// Initialize account variables if they don't exist (only when player interacts)
	if (#TRSH_DailySpend <= 0) #TRSH_DailySpend = 0;
	if (#TRSH_TotalSpent <= 0) #TRSH_TotalSpent = 0;
	
	mes .npc_name$;
	
	// Check if we have reached the daily spending limit
	if (#TRSH_DailySpend >= .daily_spending_limit) {
		mes "Ah, amigo mio! He agotado mi bolsa de monedas por hoy.";
		mes "El mercado ha sido bastante generoso, no crees?";
		mes "Vuelve otro dia, seguro tendre nuevas oportunidades para ambos.";
		close;
	}
	
	// Get weekday and check if we need to update
	.@weekday = gettime(GETTIME_WEEKDAY);
	
	// Update items if:
	// - It's a different day than last update
	// - LastTrashDay is invalid (not 0-6)
	// - Any selected item is invalid (id = 0)
	if (.@weekday != #LastTrashDay || 
		#LastTrashDay < 0 || #LastTrashDay > 6 ||
		#TodayItem0 == 0 || 
		#TodayItem1 == 0) {
		
		// Reset daily spending and update last day
		#LastTrashDay = .@weekday;
		#TRSH_DailySpend = 0;
		
		// Select one item from common items
		.@rand = rand(getarraysize(.common_items));
		#TodayItem0 = .common_items[.@rand];

		// Select one item from misc items
		.@rand = rand(1,getarraysize(.misc_items));
		#TodayItem1 = .misc_items[.@rand];
	}
	
	// Calculate prices for both items
	.@item_id0 = #TodayItem0;
	.@base_price0 = getiteminfo(.@item_id0, ITEMINFO_SELLPRICE);
	.@price0 = .@base_price0 * .increment;
	if (.@price0 < .min_price) .@price0 = .min_price;
	if (.@price0 > .max_price) .@price0 = .max_price;
	
	.@item_id1 = #TodayItem1;
	.@base_price1 = getiteminfo(.@item_id1, ITEMINFO_SELLPRICE);
	.@price1 = .@base_price1 * .increment;
	if (.@price1 < .min_price) .@price1 = .min_price;
	if (.@price1 > .max_price) .@price1 = .max_price;
	
	mes "Ah, bienvenido amigo! Dicen que la basura de un hombre es el tesoro de otro...";
	mes "Y hoy, tengo mi ojo puesto en algunos articulos particularmente interesantes.";
	next;
	
	mes .npc_name$;
	mes "Mi bolsa aun tiene " + (.daily_spending_limit - #TRSH_DailySpend) + " zeny para las aventuras de hoy.";
	next;
	
	mes .npc_name$;
	mes "Ahora, dejame decirte que ha captado mi ojo experto:";
	mes "- Un fino " + getitemname(#TodayItem0) + " - ofrecere " + .@price0 + " zeny cada uno";
	mes "- Y cualquier " + getitemname(#TodayItem1) + " - vale " + .@price1 + " zeny para mi";
	
	next;
	switch(select("Comerciar con Garrett:Preguntar sobre el negocio:Marcharse")) {
		case 1:
			mes .npc_name$;
			mes "Excelente! De cual tesoro te gustaria desprenderte?";
			next;
			
			.@menu$ = getitemname(#TodayItem0);
			.@menu$ += ":" + getitemname(#TodayItem1);
			.@menu$ += ":Cambie de opinion";
			
			.@selected = select(.@menu$) - 1;
			if (.@selected == 2) {
				mes .npc_name$;
				mes "Sin prisa, amigo. Los buenos negocios requieren reflexion.";
				close;
			}
			
			.@item_id = getd("#TodayItem" + .@selected);
			.@price = getd(".@price" + .@selected);
			
			mes .npc_name$;
			mes "Ah, el " + getitemname(.@item_id) + "! Cuantos te gustaria comerciar?";
			mes "Veo que tienes " + countitem(.@item_id) + " contigo.";
			next;
			
			input .@amount;
			if (.@amount <= 0) {
				mes .npc_name$;
				mes "Vamos, seamos serios con nuestros negocios.";
				close;
			}
			if (countitem(.@item_id) < .@amount) {
				mes .npc_name$;
				mes "Hmm... pareces estar corto de inventario.";
				mes "Solo tienes " + countitem(.@item_id) + " " + getitemname(.@item_id) + "(s).";
				mes "Negociamos con lo que tienes?";
				next;
				if(select("Si, hagamoslo:No, quiza despues") == 1) {
					.@amount = countitem(.@item_id);
				} else {
					mes .npc_name$;
					mes "Muy bien. Vuelve cuando tu inventario iguale tu ambicion!";
					close;
				}
			}
			
			.@total = .@price * .@amount;
			if (#TRSH_DailySpend + .@total > .daily_spending_limit) {
				mes .npc_name$;
				mes "Ah, me temo que eso excede mi presupuesto diario.";
				mes "Solo puedo gastar " + (.daily_spending_limit - #TRSH_DailySpend) + " zeny mas hoy.";
				mes "Quizas una cantidad menor?";
				close;
			}
			
			mes .npc_name$;
			mes "Por " + .@amount + " " + getitemname(.@item_id) + "(s), pagare " + .@total + " zeny.";
			mes "Un trato justo, no crees?";
			next;
			
			if(select("Trato hecho!:Paso") == 2) {
				mes .npc_name$;
				mes "La puerta a la oportunidad permanece abierta, amigo.";
				close;
			}
			
			delitem .@item_id, .@amount;
			Zeny += .@total;
			#TRSH_DailySpend += .@total;
			#TRSH_TotalSpent += .@total;
			
			mes .npc_name$;
			mes "Un placer hacer negocios!";
			if (#TRSH_DailySpend >= .daily_spending_limit) {
				mes "Y con eso, he alcanzado mi limite de gasto por hoy.";
				mes "Pero manana trae nuevas oportunidades!";
			} else {
				mes "Siempre estoy al acecho de mas tesoros.";
				mes "Cada dia trae nuevos intereses!";
			}
			break;
			
		case 2:
			mes .npc_name$;
			mes "Curioso sobre mis empresas? He invertido " + #TRSH_TotalSpent + " zeny en tesoros de aventureros como tu.";
			mes "Cada moneda bien gastada, diria yo!";
			break;
			
		case 3:
			mes .npc_name$;
			mes "Viajes seguros, amigo. Recuerda, la basura de hoy podria ser la riqueza de manana!";
			break;
	}
	close;
	
	/*-----------------------------------------------------
	Configuration
	-----------------------------------------------------*/
	OnInit:
		.npc_name$ = "[ ^0000FFGarrett el Recolector^000000 ]";
		// Price limits configuration
		.increment = 10;     // Price multiplier
		.min_price = 100;    // Minimum price for any item
		.max_price = 2000;   // Maximum price for any item
		.daily_spending_limit = 1243000; // 1M zeny daily limit
		
		// Item list configuration - Common items and misc items
		setarray(.common_items[0],
			511, 609, 512, 967, 962, 960, 959, 955, 951, 948,
			943, 941, 938, 937, 936, 935, 934, 932, 930, 928,
			926, 924, 920, 919, 918, 916, 915, 913, 909, 908,
			907, 746);
			
		setarray(.misc_items[0],
			511, 609, 512, 967, 962, 960, 959, 955, 951, 948,
			943, 941, 938, 937, 936, 935, 934, 932, 930, 928,
			926, 924, 920, 919, 918, 916, 915, 913, 909, 908,
			907, 746,
			1099, 1098, 1096, 1095, 1057, 1056, 1055, 1054,
			1052, 1045, 1043, 1042, 1037, 1035, 1034, 1033,
			1028, 1024, 1023, 1022, 1020, 1019, 1018, 1001,
			7568, 7507, 7347, 7345, 7319, 7317, 7262, 7197,
			7188, 7167, 7150, 7126, 7123, 7122, 7116, 7070,
			7069, 7067, 7066, 7054, 7053, 7047, 7032, 7030,
			7006, 7005, 7004, 7001);
		end;
}
