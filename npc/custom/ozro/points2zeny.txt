/*=========================================================
Cambista de Puntos y Zeny
por Mumbles
Adaptado al espanol por Ozkar
===========================================================
Descripcion:
Permite a los jugadores intercambiar puntos por zeny
y viceversa. La variable de puntos, tasa de cambio
y minimo requerido pueden configurarse facilmente.
===========================================================
Compatibilidad:
Optimizado para emuladores Hercules.
===========================================================
Changelog:
v1.0 - Version inicial.
	v1.0.1 - Agregado changelog.
	v2.0 - Adaptado al espanol con lore.
=========================================================*/

prontera,147,159,5	script	Sophie	4_F_KHELLY,{
	
	/*-----------------------------------------------------
	Function: isbigger()
	-------------------------------------------------------
	Description: Determine if first value is greater.
	-------------------------------------------------------
	Usage:
	isbigger(<value>, <value2>);
	-----------------------------------------------------*/
	function isbigger {
		if (getarg(0) < getarg(1)) {
			return false;
		}
		
		return true;
	}

	
	/*-----------------------------------------------------
	Script
	-----------------------------------------------------*/
	mes .npc_name$;
	mes "Hola, "+ strcharinfo(0) +". Puedo ayudarte";
	mes "a cambiar zeny por puntos de cash y viceversa.";
	message strcharinfo(0), "Puntos disponibles: "+ getd(.var_name$) +"";
	next;
	
	menu "Cambiar puntos por zeny:Cambiar zeny por puntos:Nada, gracias", -;
	
	mes .npc_name$;
	switch (@menu) {
		case 1:
			mes "Cuantos puntos deseas convertir a zeny?";
			message strcharinfo(0), "Tasa: "+ .exchange_rate +" zeny por punto.";
			next;
			
			do {
				message strcharinfo(0), "Minimo: "+ .exchange_min +" puntos.";
				input .@points;
			} while (.@points < .exchange_min);
			
			mes .npc_name$;
			
			// Insufficient funds
			if (!isbigger(getd(.var_name$), .@points)) {
				mes "No tienes "+ .@points +" puntos disponibles.";
				close;
			}
			
			.@payout = .@points * .exchange_rate;
			
			mes "Te doy "+ .@payout +" zeny por "+ .@points +" puntos.";
			mes "De acuerdo?";
			next;
			
			menu "Si:No", -;
			
			mes .npc_name$;
			switch (@menu) {
				case 1:
					mes "Hecho. Aqui esta tu zeny.";
					
					// Update balance
					Zeny += .@payout;
					setd(.var_name$), getd(.var_name$) - .@points;
					
					message strcharinfo(0), "Zeny: "+ Zeny +" | Puntos: "+ getd(.var_name$) +"";
					break;
			
				default:
					mes "Como prefieras.";
					break;
			}
			
			break;
			
		case 2:
			mes "Cuantos puntos deseas comprar?";
			message strcharinfo(0), "Tasa: "+ .exchange_rate +" zeny por punto.";
			next;
			
			do {
				message strcharinfo(0), "Minimo: "+ .exchange_min +" puntos.";
				input .@points;
			} while (.@points < .exchange_min);
			
			.@cost = .@points * .exchange_rate;
			
			mes .npc_name$;
			
			// Insufficient funds
			if (!isbigger(Zeny, .@cost)) {
				mes "No tienes suficiente zeny.";
				mes "Necesitas "+ .@cost +" zeny.";
				break;
			}
			
			mes "Te doy "+ .@points +" puntos por "+ .@cost +" zeny.";
			mes "De acuerdo?";
			next;
			
			menu "Si:No", -;
			
			mes .npc_name$;
			switch (@menu) {
				case 1:
					mes "Hecho. Aqui estan tus puntos.";
					
					// Update balance
					Zeny -= .@cost;
					setd(.var_name$), getd(.var_name$) + .@points;
					
					message strcharinfo(0), "Puntos: "+ getd(.var_name$) +" | Zeny: "+ Zeny +"";
					break;
			
				default:
					mes "Como prefieras.";
					break;
			}
			
			break;
			
		default:
			mes "Vuelve cuando necesites cambiar dinero.";
			break;
	}
	
	close;
	

	/*-----------------------------------------------------
	Configuracion
	-----------------------------------------------------*/
	OnInit:
		.npc_name$ = "[^0000FSSophie^000000]";
		.var_name$ = "#CASHPOINTS";	// Nombre de variable de puntos
		.exchange_rate = 20000;		// Cantidad de zeny por punto
		.exchange_min = 10;			// Minimo puntos para intercambiar
		
		// Titulo del chat
		// waitingroom strnpcinfo(1), 0;
		end;

}

geffen,97,72,5	duplicate(Sophie)	Sophie#geffen	4_F_KHELLY
